apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

def pom = new XmlSlurper().parse(file('./pom.xml'))

group = pom.parent.groupId.text().toString()
version = pom.parent.version.text().toString()

description = pom.description.text().toString()

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    // At this point, it is not given that any artifact from the Maven build can be found in a repository.
    def artifact = Boolean.getBoolean('net.bytebuddy.misc.extras') ? 'byte-buddy' : 'byte-buddy-dep'
    def location = "../${artifact}/target/${artifact}-${version}.jar"
    logger.info("Using ${artifact} as Byte Buddy dependency")
    if (!new File(location).exists()) {
        throw new GradleException("Byte Buddy jar does not exist: $location");
    }
    compile files(location)
    // When using the non-shaded dependency,
    if (!Boolean.getBoolean('net.bytebuddy.misc.extras')) {
        def asmVersion = new XmlSlurper().parse(file('../pom.xml')).properties.'version.asm'
        compile group: 'org.ow2.asm', name: 'asm', version: asmVersion
        compile group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion
    }
    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

// The extras switch determines if the Maven build should create a javadoc jar.
if (Boolean.getBoolean('net.bytebuddy.misc.extras')) {
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
} else {
    task javadocJar {
        logger.info('javadoc is only generated if net.bytebuddy.test.extras is set to true')
    }
}

test {
    systemProperty('net.bytebuddy.test.version', version)
    systemProperty('net.bytebuddy.test.integration', Boolean.getBoolean('net.bytebuddy.test.integration'))
}
