apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "org.encog"
archivesBaseName = "encog-core"
version = "3.4"


buildscript {
  repositories {
    jcenter()
  }
}

sourceCompatibility=1.7
targetCompatibility=1.7

configurations {
    deployerJars
}

repositories {
   mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hsqldb:hsqldb:2.0.0'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.1'
}

task createWrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Example Application'
        packaging 'jar'
		artifactId 'encog-core' // optionally artifactId can be defined here
        description 'Encog Machine Learning Framework.'
        url 'http://www.heatonresearch.com/encog/'

        scm {
		    connection 'https://github.com/encog/encog-java-core'
    		developerConnection 'git@github.com:encog/encog-java-core.git'
    		url 'http://code.google.com/p/encog-java/source/browse/'

        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'jeffheatondotcom'
            name 'Jeff Heaton'
            email 'jheaton@heatonresearch.com'
          }
        }
      }
    }
  }
}

task getVersion << {
  println '3.4'
}
